apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 33
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    buildFeatures {
        viewBinding true
    }
    namespace 'io.github.wulkanowy.materialchipsinput'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

def configGroupId = 'com.github.wulkanowy'
def configArtifactId = 'material-chips-input'

project.afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact androidSourcesJar

                groupId = configGroupId
                artifactId = configArtifactId
                version = libs.versions.library.get()
            }
        }
    }
}

dependencies {
    implementation libs.androidx.appcompat
    implementation libs.androidx.core
    implementation libs.androidx.recyclerview

    implementation libs.material.components
    implementation libs.circleimageview
    implementation libs.fuzzywuzzy
}
